global Class, V2 // core.trm
global GameMap // map.trm
global fov_map, message, player, entities // state.trm
global pathfind // pathfind.trm
global addNumber // particles.trm

var animation_time = 6

global fn getBlockingEntitiesAt(dx) {
	for (e in all(entities)) {
		if (e.blocks && e.pos == dx) {
			return e
		}
	}
}

global Fighter = Class{
	method construct(hp, defence, power) {
		self.max_hp = hp
		self.hp = hp
		self.defence = defence
		self.power = power
	},

	method takeDamage(amount) {
		self.hp -= amount
		if (self.hp < 0 && self != player) {
			self.owner.sprite += 16
			self.owner.blocks = false
			self.owner.name = "Remains of "..self.owner.name
			self.owner.fighter = null
			self.owner.ai = null
		}
	},

	method attack(target) {
		var damage = self.power - target.fighter.defence
		target.fighter:takeDamage(damage)
		addNumber(damage, 8, target.pos * 8 + V2(2, 0), V2(0, -1), 120)

		var init = self.owner.pos
		var dx = target.pos
		var t = 0
		while (t < animation_time/2) {
			yield()
			if (btnp() != 0) {
				break
			}
			t += 1
			self.owner.pos = init + (dx - init) * (t / animation_time)
		}
		while (t >= 0) {
			yield()
			if (btnp() != 0) {
				break
			}
			t -= 1
			self.owner.pos = init + (dx - init) * (t / animation_time)
		}
		self.owner.pos = init
	},
}

global BasicMonster = Class{
	method takeTurn() {
		if (fov_map:get(self.owner.pos)) {
			if (self.owner:distanceTo(player) > 1) {
				self.owner:moveTowards(player.pos)
			} else if (player.fighter.hp > 0) {
				self.owner.fighter:attack(player)
			}
		}
	},
}

fn blocks(pos) {
	return GameMap:isBlocked(pos)
}

global Entity = Class{
	method construct(pos, sprite, name, blocks, fighter, ai) {
		self.pos = pos
		self.sprite = sprite
		self.name = name
		self.blocks = blocks
		self. fighter = fighter
		self.ai = ai

		if (self.fighter) {
			self.fighter.owner = self
		}
		if (self.ai) {
			self.ai.owner = self
		}
	},

	method move(dx) {
		var init = self.pos
		var t = 0
		while (t < animation_time) {
			yield()
			if (btnp() != 0) {
				break
			}
			t += 1
			self.pos = init + (dx - init) * (t/animation_time)
		}
		self.pos = dx
	},

	method moveTowards(tpos, entities) {
		var path = pathfind(self.pos, tpos, blocks)
		if (path && #path >= 3 && !getBlockingEntitiesAt(path[2]:unpack())) {
			self:move(path[2])
		}
	},

	method distanceTo(other) {
		return #(other.pos - self.pos)
	},

	method draw() {
		spr(self.sprite, (self.pos * 8):unpack())
	},
}
